/datum/preference_middleware/stats
	action_delegations = list(
		"increase_stat" = PROC_REF(increase_stat),
		"decrease_stat" = PROC_REF(decrease_stat),
		"reset_stats" = PROC_REF(reset_stats)
	)

/datum/preference_middleware/stats/get_ui_static_data(mob/user)
	if (preferences.current_window != PREFERENCE_TAB_CHARACTER_PREFERENCES)
		return list()

	var/list/data = list()
	data["static_stats"] = list()
	for(var/stat_type in GLOB.public_storyteller_stats)
		var/datum/st_stat/stat = GLOB.public_storyteller_stats[stat_type]
		var/list/stat_data = list()
		stat_data["name"] = stat.name
		stat_data["desc"] = stat.description
		stat_data["editable"] = stat.editable
		stat_data["category"] = stat.category
		stat_data["subcategory"] = stat.subcategory
		stat_data["max_score"] = stat.max_score
		data["static_stats"][stat_type] = stat_data
	return data

/datum/preference_middleware/stats/get_ui_data(mob/user)
	var/list/data = list()
	data["stats"] = preferences.storyteller_stats
	data["points"] = preferences.storyteller_stat_points
	return data

/datum/preference_middleware/stats/proc/increase_stat(list/params, mob/user)
	var/stat_path = text2path(params["stat"])
	var/datum/st_stat/public_stat = GLOB.public_storyteller_stats[stat_path]
	if(!public_stat)
		return FALSE
	if(preferences.storyteller_stats[stat_path] >= public_stat.max_score)
		return FALSE

	if(preferences.storyteller_stats[stat_path] < public_stat.starting_score)
		preferences.storyteller_stat_points[public_stat.abstract_type] += 1
	if(!preferences.storyteller_stat_points[public_stat.abstract_type] && !preferences.storyteller_stat_points[STAT_FREEBIE_POINTS])
		return FALSE
	if((preferences.storyteller_stat_points[public_stat.abstract_type] >= public_stat.max_score) && public_stat.count_bonus_score)
		return FALSE
	preferences.storyteller_stats[stat_path] += 1
	if(preferences.storyteller_stat_points[public_stat.abstract_type] > 0 && (preferences.storyteller_stats[stat_path] <= public_stat.max_level_before_freebie_points))
		preferences.storyteller_stat_points[public_stat.abstract_type] -= 1
	else
		if((preferences.storyteller_stat_points[STAT_FREEBIE_POINTS] - public_stat.freebie_point_cost) < 0)
			preferences.storyteller_stats[stat_path] -= 1
			return TRUE
		preferences.storyteller_stat_points[STAT_FREEBIE_POINTS] -= public_stat.freebie_point_cost
	return TRUE

/datum/preference_middleware/stats/proc/decrease_stat(list/params, mob/user)
	var/stat_path = text2path(params["stat"])
	var/datum/st_stat/public_stat = GLOB.private_storyteller_stats[stat_path]
	if(!public_stat)
		return FALSE
	if(preferences.storyteller_stats[stat_path] < 0)
		return FALSE
	var/datum/st_stat/parent_stat_type = public_stat.abstract_type

	preferences.storyteller_stats[stat_path] -= 1
	if(preferences.storyteller_stats[stat_path] < public_stat.starting_score)
		preferences.storyteller_stat_points[public_stat.abstract_type] -= 1
	if(preferences.storyteller_stat_points[public_stat.abstract_type] < initial(parent_stat_type.points))
		preferences.storyteller_stat_points[public_stat.abstract_type] += 1
	else
		preferences.storyteller_stat_points[STAT_FREEBIE_POINTS] += public_stat.freebie_point_cost
	return TRUE

/datum/preference_middleware/stats/proc/reset_stats(list/params, mob/user)
	preferences.storyteller_stats = SSstats.sanitize_stat_list()
	preferences.storyteller_stat_points = SSstats.sanitize_points_list()
	return TRUE
